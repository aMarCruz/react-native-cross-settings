// Top-level build file where you can add configuration options common to all sub-projects/modules.
import groovy.json.JsonSlurper

// dynamically retrieve version from package.json
def getPackageVersion() {
    return new JsonSlurper().parse(file('../package.json'), 'utf-8').version
}

def safeExtGet(prop, fallback) {
    rootProject.ext.hasProperty(prop) ? rootProject.ext.get(prop) : fallback
}

def _buildToolsVersion  = safeExtGet('buildToolsVersion', '26.0.3')
def _compileSdkVersion  = safeExtGet('compileSdkVersion', 26)
def _targetSdkVersion   = safeExtGet('targetSdkVersion', 26)
def _minSdkVersion      = safeExtGet('minSdkVersion', 16)

buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://maven.google.com/'
            name 'google'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'
    }
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion _compileSdkVersion
    buildToolsVersion _buildToolsVersion
    defaultConfig {
        minSdkVersion _minSdkVersion
        targetSdkVersion _targetSdkVersion
        versionCode 1
        versionName getPackageVersion()
    }

    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven {
        url 'https://maven.google.com/'
        name 'google'
    }
    maven {
        url "$rootDir/../node_modules/react-native/android"
        name 'React Native (local)'
    }
}

dependencies {
    compile 'com.facebook.react:react-native:+'
}

task customClean(type: Delete) {
    delete rootProject.buildDir
}
clean.dependsOn customClean
